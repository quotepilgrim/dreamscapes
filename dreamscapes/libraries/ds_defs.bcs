#library "ds_defs"
#import "zcommon.bcs"

// constants
enum {
	INT32_MAX = 0x7FFFFFFF,
	ONE_TICK = 1873,
	SHARD_PICKUP = 199,
	DIALOGUE = 277,
};

// bit flags
enum {
	GOT_SHARD		= 1,
	MSG_DOOR_SEEN	= 1 << 1,
	HELP_BED_SEEN	= 1 << 2,
	GOT_NEXUS_ITEM	= 1 << 3,
	HELP_LTOP_SEEN	= 1 << 4,
};

//
global int 1: bit_flags;
global int 2: liaju_counter;
global int 3: HAS_SHARD[14];

str SHARDS[] = {
	"CrescentShard",
	"StarShard",
	"MeteorShard",
	"LightningShard",
	"FloodShard",
	"EmberShard",
	"CrystalShard",
	"FlowShard",
	"GravityShard",
	"ChaosShard",
	"VoidShard",		
	"MatterShard",
	"ShellShard",
	"SoulShard"
};

// functions
int CountShards() {
	int count = 0;
	foreach (let str shard; SHARDS) {
		if (CheckInventory(shard)) {
			count++;
		}
	}
	return count;
}

void SetInventory(str item, int amount) {
	TakeInventory(item, INT32_MAX);
	GiveInventory(item, amount);
}

int CheckEventFlag(int flag) {
	return bit_flags & flag;
}

void SetEventFlag(int flag) {
	bit_flags |= flag;
}

void UnsetEventFlag(int flag) {
	bit_flags &= ~flag;
}

void ToggleEventFlag(int flag) {
	bit_flags ^= flag;
}



























